/* add/subtract reward points for a user name.
 * input is user name 
 * and +/- point delta.
 *
 * output: Return value - 0 - success 
 *                        20001 - No such user name
 */
IF EXISTS
 (SELECT name FROM sysobjects WHERE name = 'updateUserPointsByName' AND type = 'P') 
 DROP PROC updateUserPointsByName
GO  
CREATE PROC updateUserPointsByName (
    @nickName	VPuserID,
    @points	int
)
AS
BEGIN
  DECLARE @userID userIdentifier
  DECLARE @lastError int

  BEGIN TRAN
    
    SELECT @userID = NULL
    SELECT @userID = userID
      FROM  users
      WHERE nickName = @nickName
      AND registrationMode = 2
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN
      RETURN @lastError
    END

    IF @userID IS NULL
    BEGIN
      ROLLBACK TRAN
      RETURN 20001
    END

    IF EXISTS (SELECT * FROM userPoints WHERE userID = @userID)
    BEGIN
      UPDATE userPoints SET points = points + @points
        WHERE userID = @userID
    END
    ELSE BEGIN
      INSERT userPoints (userID, points)
        VALUES(@userID, @points)
    END
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN
      RETURN @lastError
    END

  COMMIT TRAN

  EXEC addAcctHistory @userID, 80, @points

END
GO
