/* update a user account record */
/* input:  account owner name, payment status, timestamp (optional)

   paymentStatus:
	0 = OK
	1 = unconfirmed
	2 = failed
	3 = waiting for additional payment
	4 = pending eCheck
	5 = free trial

   output: Return value - 0 - success 
                          20001 - No such account existed in database
*/
CREATE PROC updatePaymentStatus ( @accountID userIdentifier, @paymentStatus tinyInt, @lastUpdated VpTime = NULL )
AS
BEGIN
  DECLARE @lastError int
  DECLARE @aid userIdentifier
  DECLARE @diffFromGMT int
  DECLARE @oldStatus tinyint
  
  IF @lastUpdated = NULL
    SELECT @lastUpdated = getdate()

  BEGIN TRAN
    
    SELECT @diffFromGMT = gmt
      FROM getGMT
    IF @diffFromGMT IS NULL
      SELECT @diffFromGMT = 0
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN 
      RETURN @lastError
    END

    SELECT @lastUpdated = dateadd( hour, (-1) * @diffFromGMT, @lastUpdated )

    SELECT @aid = accountID,
           @oldStatus = paymentStatus
      FROM userAccounts
      WHERE accountID = @accountID
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN
      RETURN @lastError
    END

    IF @accountID != @aid
    BEGIN
      ROLLBACK TRAN
      RETURN 20001
    END


    /*
     * If old status not OK and waiting for additional pmt,
     * preserve the old status
     */
    IF @oldStatus !=0 AND @paymentStatus = 3
    BEGIN
      COMMIT TRAN
      RETURN 0
    END

    UPDATE userAccounts
      SET paymentStatus = @paymentStatus,
          lastUpdated = @lastUpdated
      WHERE accountID = @accountID
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN
      RETURN @lastError
    END

  COMMIT TRAN

  /*
   * add history record if applicable
   */
  IF @oldStatus = 5 AND @paymentStatus = 0
  BEGIN
    EXEC addAcctHistory @accountID, 12, 0
  END
  IF @paymentStatus = 2
  BEGIN
    EXEC addAcctHistory @accountID, 41, @oldStatus
  END
  
  RETURN 0
END
GO
