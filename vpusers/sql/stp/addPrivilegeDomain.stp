/* add a privilege domain for a specific user ID

   INPUT  : nickName, domain
   OUTPUT : return value - 0 - successfull
                           20003 - tried to add privilege domain to a user in
                                   local registration mode, but this user
                                   was not found in the local registration
                           20001 - domain name already specified for this user
                           
        
*/
CREATE PROC addPrivilegeDomain
(
  @nickName VPuserID,
  @domain UrlType 
)
AS
BEGIN
  DECLARE @userID userIdentifier
  DECLARE @auxDbAllowed bit
  DECLARE @lastError int
  DECLARE @retVal int
  DECLARE @domainFound int
  SELECT @domainFound = 0
  SELECT @auxDbAllowed = 0
  
  BEGIN TRAN addPrivilegeDomain
    
    SELECT @auxDbAllowed = intValue
      FROM configurationKeys
      WHERE ( keyName = "auxDbAllowed" )
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN
      RETURN @lastError
    END
    
    /* get user ID, or create an entry
       for this user if none exists yet */
    EXEC @retVal = updateUser @userID OUTPUT, @nickName, 2, @auxDbAllowed
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN addPrivilege
      RETURN @lastError
    END
    
    IF @userID = 0
    BEGIN
      /* tried to add privilege domain to a user in
         local registration mode, but this user
         was not found in the local registration */
      ROLLBACK TRAN addPrivilegeDomain
      RETURN 20003
    END
    
    IF ( @retVal NOT IN ( 20001, 0 ) )
    BEGIN
      ROLLBACK TRAN addPrivilegeDomain
      RETURN @retVal
    END
    
    SELECT @domainFound = 1
      FROM privilegeDomains
      WHERE ( userID = @userID ) AND
            ( domain = @domain )
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN addPrivilegeDomain
      RETURN @lastError
    END
    
    IF ( @domainFound = 1 )
    BEGIN
      /* this privilege domain was already
         specified for this user */
      ROLLBACK TRAN addPrivilegeDomain
      RETURN 20001
    END
    
    INSERT privilegeDomains ( userID, domain )
      VALUES ( @userID, @domain )
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN addPrivilegeDomain
      RETURN @lastError
    END
  COMMIT TRAN addPrivilegeDomain
END
GO
