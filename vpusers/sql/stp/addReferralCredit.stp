/*
 * Give credit for a referral to an account. Usually this is done during
 * new user registration, but there may be reason to do it retroactively.
 *
 * Input: account ID of the new user
 *        account ID of the referrer
 *
 * Output: 0
 *         20001 no such referee
 *         20002 no such referrer
 */
CREATE PROC addReferralCredit
( 
  @accountID		userIdentifier,
  @referredByID		userIdentifier
)
AS
BEGIN
  DECLARE @lastError 	int
  DECLARE @referralType	tinyint

  BEGIN TRAN
    IF NOT EXISTS (SELECT * FROM userAccounts WHERE accountID = @accountID)
    BEGIN
      ROLLBACK TRAN
      RETURN 20001
    END
    IF NOT EXISTS (SELECT * FROM userAccounts WHERE accountID = @referredByID)
    BEGIN
      ROLLBACK TRAN
      RETURN 20002
    END

    SELECT @referralType = type 
      FROM referralPrefs
      WHERE accountID = @referredByID

    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN 
      RETURN @lastError
    END

    IF @referralType IS NULL
    BEGIN
      SELECT @referralType = 255	-- unenrolled referrer
    END

    UPDATE userAccounts
      SET referredByID = @referredByID
      WHERE accountID = @accountID

    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN 
      RETURN @lastError
    END

    IF EXISTS (SELECT * FROM referrals WHERE accountID = @accountID)
    BEGIN
      UPDATE referrals
        SET referredByID = @referredByID,
            type = @referralType
        WHERE accountID = @accountID
    END
    ELSE BEGIN
      INSERT INTO referrals
        (accountID, referredByID, type)
        VALUES (@accountID, @referredByID, @referralType)
    END
      
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN 
      RETURN @lastError
    END
  COMMIT TRAN
END
GO
