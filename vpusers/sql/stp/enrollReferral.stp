/* 
 * Enroll in the referral bonus program
 *
 * input: accountID, flag 
 *		0 = $1 monthly payment option
 *		1 = one time payment option
 *
 *  output: Return value - 0 - success 
 *	 	20001 - unknown accountID
 * 	  	20002 - invalid option
 */
CREATE PROC enrollReferral ( 
	@accountID	userIdentifier,
	@type		tinyint
)
AS
BEGIN
  DECLARE @lastError	int
  DECLARE @diffFromGMT int
  DECLARE @currentDate VpTime

  BEGIN TRAN
    
    IF @type != 0 AND @type != 1
    BEGIN
      ROLLBACK TRAN
      RETURN 20002
    END

    SELECT @diffFromGMT = gmt
      FROM getGMT
    IF @diffFromGMT IS NULL
      SELECT @diffFromGMT = 0
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN addName
      RETURN @lastError
    END

    SELECT @currentDate = dateadd( hour, (-1) * @diffFromGMT, getdate() )

    IF NOT EXISTS (SELECT * FROM userAccounts WHERE accountID = @accountID)
    BEGIN
      ROLLBACK TRAN
      RETURN 20001
    END

    IF EXISTS (SELECT * FROM referralPrefs WHERE accountID = @accountID)
    BEGIN
      UPDATE referralPrefs
        SET type = @type,
            enrollDate = @currentDate
        WHERE accountID = @accountID

      SELECT @lastError = @@error
      IF @lastError != 0
      BEGIN
        ROLLBACK TRAN
        RETURN @lastError
      END
    END
    ELSE BEGIN

      UPDATE userAccounts
        SET enrolledReferrer = 1
        WHERE accountID = @accountID

      SELECT @lastError = @@error
      IF @lastError != 0
      BEGIN
        ROLLBACK TRAN
        RETURN @lastError
      END

      INSERT INTO referralPrefs (accountID, type, enrollDate)
        VALUES (@accountID, @type, @currentDate )
       
      SELECT @lastError = @@error
      IF @lastError != 0
      BEGIN
        ROLLBACK TRAN
        RETURN @lastError
      END

    END
  COMMIT TRAN
  RETURN 0
END
GO
