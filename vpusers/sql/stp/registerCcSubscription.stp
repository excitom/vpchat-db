/* add a new subscription record for a credit card payer
   output: return value -
           negative value - DB failure, 
           0 - success, new subscription
           1 - success, updated subscription
           20001 - unknown accountID
           20002 - account closed or suspended
*/
CREATE PROC registerCcSubscription
( 
  @accountID userIdentifier,
  @ccID userIdentifier,
  @creationDate VpTime = NULL,
  @eCheck bit = 0,
  @autoRenew bit = 1
)
AS
BEGIN
  DECLARE @lastError int
  DECLARE @diffFromGMT int
  DECLARE @aid userIdentifier
  DECLARE @accountStatus tinyint
  DECLARE @type tinyint
  DECLARE @rc int

  IF @eCheck = 1
    SELECT @type = 3	-- account type 3 = electronic check
  ELSE
    SELECT @type = 2	-- account type 2 = credit card

  BEGIN TRAN
    SELECT @diffFromGMT = gmt
      FROM getGMT
    IF @diffFromGMT IS NULL
      SELECT @diffFromGMT = 0
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN registerNewUser
      RETURN @lastError
    END

    IF @creationDate IS NULL
      SELECT @creationDate = getdate()

    SELECT @creationDate = dateadd( hour, (-1) * @diffFromGMT, @creationDate )

    SELECT @accountStatus = accountStatus
      FROM userAccounts
      WHERE accountID = @accountID

    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN 
      RETURN @lastError
    END

    IF @accountStatus IS NULL
    BEGIN
      ROLLBACK TRAN 
      RETURN 20001
    END

    IF @accountStatus = 3 OR @accountStatus = 4
    BEGIN
      ROLLBACK TRAN 
      RETURN 20002
    END

    /*
     * if a person has a credit card subscription, then changes to
     * eCheck (or vice versa) we automatically deactivate the former.
     */
    IF EXISTS (SELECT * FROM subscriptions WHERE accountID = @accountID AND type > 1)
    BEGIN
      UPDATE subscriptions SET autoRenew = 0 WHERE accountID = @accountID AND type > 1

      SELECT @lastError = @@error
      IF @lastError != 0
      BEGIN
        ROLLBACK TRAN 
        RETURN @lastError
      END
    END
 
    SELECT @aid = accountID
      FROM subscriptions
      WHERE accountID = @accountID
      AND   type = @type

    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN 
      RETURN @lastError
    END

    /* new subscription */
    IF @aid IS NULL
    BEGIN
      INSERT INTO subscriptions
        (accountID, type, creationDate, ccID, autoRenew)
        VALUES (@accountID, @type, @creationDate, @ccID, @autoRenew)

      SELECT @lastError = @@error
      IF @lastError != 0
      BEGIN
        ROLLBACK TRAN 
        RETURN @lastError
      END
      SELECT @rc = 0
    END
    /* updating an existing subscription with ccID,
     * since user may be using a different card
     */
    ELSE
    BEGIN
      UPDATE subscriptions
        SET ccID         = @ccID,
            autoRenew    = @autoRenew
        WHERE accountID  = @accountID
        AND type = @type

      SELECT @lastError = @@error
      IF @lastError != 0
      BEGIN
        ROLLBACK TRAN 
        RETURN @lastError
      END
      SELECT @rc = 1
   END
   COMMIT TRAN

   /*
    * add a history record
    */
   DECLARE @reason int
   IF @eCheck = 1
   BEGIN
     SELECT @reason = 23
   END
   ELSE BEGIN
     SELECT @reason = 22
   END
   EXEC addAcctHistory @accountID, @reason, @autoRenew, NULL

   RETURN @rc
END
GO 
