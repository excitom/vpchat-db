/* change user password */
/* input:  user name, new password 
   output: none */
CREATE PROC changePassword ( @nickName VPuserID, @password VPPassword )
AS
BEGIN
  /* turn all user ID to lower case, to get case insensitive comparisons */
  SELECT @nickName = lower(@nickName)
  DECLARE @lastError int
  DECLARE @userID userIdentifier
  DECLARE @email longName
  DECLARE @oldPassword VPPassword
  
  BEGIN TRAN changePassword
    SELECT @userID = userID
      FROM users
      WHERE ( nickName = @nickName ) AND
            ( users.registrationMode = 2 )
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN changePassword
      RETURN @lastError
    END
    
    IF ( @userID IS NULL )
    BEGIN
      ROLLBACK TRAN changePassword
      RETURN
    END
    
    SELECT @oldPassword = password, @email = email
      FROM registration
      WHERE ( userID = @userID )
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN changePassword
      RETURN @lastError
    END
    
    IF ( @oldPassword IS NULL ) OR 
       ( @oldPassword = @password )
    BEGIN
      /* nothing to do - return */
      ROLLBACK TRAN changePassword
      RETURN
    END
    
    /* change the password in the database to the new password */
    UPDATE registration
      SET password = @password
      WHERE ( userID = @userID )
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN changePassword
      RETURN @lastError
    END
    
    /* add record for changed password */
    INSERT passwordChanges
      VALUES ( @nickName, @email, @password )
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN changePassword
      RETURN @lastError
    END
    
  COMMIT TRAN changePassword  
END
GO 
