/*
 * Set up email forwarding for a user name
 *
 * Input: user name, email address
 *
 * Output: 0 = success
 *         20000 = unknown name
 */
CREATE PROC addEmailForwarding ( 
	@serviceID	userIdentifier,
	@nickName	VPuserID,
	@email		longName,
	@locked		bit
)
AS
BEGIN
  DECLARE @lastError	int
  DECLARE @userID	userIdentifier
  DECLARE @accountID	userIdentifier

  BEGIN TRAN
    SELECT @userID = users.userID,
           @accountID = accountID
      FROM users, registration
      WHERE nickName = @nickName
      AND registrationMode = 2
      AND locked = 0
      AND deleteDate IS NULL
      AND accountID != 0
      AND users.userID = registration.userID
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN 
      RETURN @lastError
    END

    IF @userID IS NULL
    BEGIN
      ROLLBACK TRAN
      RETURN 20000
    END

    IF EXISTS (SELECT * FROM emailForwarding WHERE userID = @userID)
    BEGIN
      UPDATE emailForwarding
        SET serviceID = @serviceID,
            email = @email,
            locked = @locked
        WHERE userID = @userID
    END
    ELSE BEGIN
      INSERT emailForwarding (serviceID, userID, email, locked)
        VALUES (@serviceID, @userID, @email, @locked)
    END
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN 
      RETURN @lastError
    END

    /*
     * trigger a service change
     */
    INSERT emailForwardingChanges (accountID) VALUES (@accountID)
    
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN 
      RETURN @lastError
    END

  COMMIT TRAN
END
GO
