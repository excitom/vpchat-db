/*
 * Add/update an HTML link
 *
 * The descriptiom column is used to group related links.
 * The linkOrder column is used to sort the links within a group.
 * The description/URL combination should be unique. If you add with
 * a non-unique description/URL, it updates instead of inserting.
 *
 * OUTPUT: 0 = success
 *         20001 = non-existant category
 *
 */
CREATE PROC addLink (
  @description  VARCHAR(128),
  @URL          VARCHAR(255),
  @title        VARCHAR(255),
  @linkOrder	tinyint=127,	-- default, put at the bottom
  @popup	bit=0,		-- default, not a popup
  @breakAfter	bit=0		-- default, no break after
)
AS
BEGIN
  DECLARE @lastError int
  DECLARE @category categoryIdentifier
  BEGIN TRAN
    SELECT @category = category
      FROM linkCategories
      WHERE description = @description
      
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN 
      RETURN @lastError
    END
      
    IF ( @category IS NULL )
    BEGIN
      ROLLBACK TRAN 
      RETURN 20001
    END

    IF EXISTS (SELECT * FROM links WHERE category = @category AND URL = @URL)
    BEGIN
      UPDATE links
        SET title = @title,
            linkOrder = @linkOrder,
            popup = @popup,
            breakAfter = @breakAfter
        WHERE category = @category AND URL = @URL
    END
    ELSE BEGIN
      INSERT links (category, URL, title, linkOrder, popup, breakAfter)
        VALUES (@category, @URL, @title, @linkOrder, @popup, @breakAfter)
    END
      
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN 
      RETURN @lastError
    END

  COMMIT
END
GO
