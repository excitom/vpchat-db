/*
 * Update a user home page
 *
 * The change parameter indicates what is being updated.
 * L = Lock status
 *
 * Depending on the change parameter, either an integer or string
 * parameter may be passed.
 *
 * output: return value - 0 if successful
 *			  20001 not found
 *                        20002 unknown change type
 */
CREATE PROC updateHomePage
(
  @userID               userIdentifier,
  @change		char(1),
  @intVal		int = NULL,
  @strVal		longName = NULL
)
AS
BEGIN
  DECLARE @lastError int
  DECLARE @diffFromGMT int

  BEGIN TRAN
    SELECT @diffFromGMT = gmt
    FROM vpusers..getGMT
   
    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN
      RETURN @lastError
    END

    IF EXISTS (SELECT * FROM homePages WHERE userID = @userID)
    BEGIN

      IF @change = 'L'
      BEGIN
        DECLARE @deleted bit
        SELECT @deleted = deleted
          FROM homePages
          WHERE userID = @userID
   
        SELECT @lastError = @@error
        IF @lastError != 0
        BEGIN
          ROLLBACK TRAN
          RETURN @lastError
        END

        /*
         * Ignore if page is deleted
         */
        IF @deleted = 1
        BEGIN
          ROLLBACK TRAN
          RETURN 0
        END

        UPDATE homePages SET locked = @intVal WHERE userID = @userID
   
        SELECT @lastError = @@error
        IF @lastError != 0
        BEGIN
          ROLLBACK TRAN
          RETURN @lastError
        END

      END
      ELSE BEGIN
        ROLLBACK TRAN
        RETURN 20002
      END

      /* add a change record */

      INSERT homePageChanges (time, userID, change, intVal, strVal)
        VALUES (dateadd(hour, (-1) * @diffFromGMT, getdate()),
                @userID, @change, @intVal, @strVal)
        
      SELECT @lastError = @@error
      IF @lastError != 0
      BEGIN
        ROLLBACK TRAN
        RETURN @lastError
      END
    END
    ELSE
    BEGIN
      ROLLBACK TRAN
      RETURN 20001
    END
   
  COMMIT TRAN
  RETURN 0

END
GO
GRANT EXECUTE ON updateHomePage TO vpusr
GO
