/*
 * Set the prize percentage for one place in a tournament.
 *
 * Input: Tournament ID, place, prize percentage
 *        If place = 0, reset all 
 *
 * Output: 0 = success
 *         20000 = no such tournament
 *         20001 = no prize associated with this tournament
 *         20002 = percentage exceeds 100
 *         20003 = tournament finished or cancelled
 *         20004 = no such player
 */
IF EXISTS(SELECT name FROM sysobjects WHERE name='setPrizePct' AND type='P')
 DROP PROC setPrizePct
GO
CREATE PROC setPrizePct (
  @tournID	userIdentifier,
  @userID	userIdentifier,
  @pct		tinyint
)
AS
BEGIN
  DECLARE @buyIn      int
  DECLARE @addedValue int
  DECLARE @status     tinyint
  DECLARE @players    smallint
  DECLARE @lastError  int
  DECLARE @allocated  double precision
  DECLARE @percentage double precision

  SELECT @percentage = convert(float, @pct) / 100

  SELECT @buyIn      = buyIn,
         @addedValue = addedValue,
         @status     = status,
         @players    = players
    FROM tournaments WHERE tournID = @tournID

  IF @@rowcount = 0
    RETURN 20000

  IF @addedValue + @buyIn = 0
    RETURN 20001

  IF @status != 0x40
    RETURN 20003

  BEGIN TRAN

    SELECT @allocated = SUM(prizeShare)
      FROM tournamentPlayers
      WHERE tournID = @tournID
      AND        td = 0

    IF @allocated IS NOT NULL
    AND @allocated + @percentage > 1.0
    BEGIN
      ROLLBACK TRAN
      RETURN 20002
    END

    UPDATE tournamentPlayers
        SET prizeShare = @percentage
        WHERE  tournID = @tournID
        AND     userID = @userID
        AND         td = 0

    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN
      RETURN @lastError
    END

  COMMIT TRAN
END
GO
