/*
 * Add/update note text in a game ladder
 *
 * Input: Ladder ID and note text
 *
 * Output: 0 = success
 *         20000 = ladder not found
 */
CREATE PROC addLadderNote ( 
	@notifyID	userIdentifier,
	@idx		tinyint,
	@sourceCode	varchar(255)
)
AS
BEGIN
  DECLARE @lastError	int
  DECLARE @diffFromGMT 	int
  DECLARE @now    	VpTime

  SELECT @diffFromGMT = gmt
    FROM vpusers..getGMT
  IF @diffFromGMT IS NULL
    SELECT @diffFromGMT = 0
    
  SELECT @lastError = @@error
  IF @lastError != 0
  BEGIN
    RETURN @lastError
  END
  SELECT @now = dateadd( hour, (-1) * @diffFromGMT, getdate() )

  BEGIN TRAN

    IF NOT EXISTS( SELECT * FROM ladders WHERE notifyID = @notifyID)
    BEGIN
      ROLLBACK TRAN
      RETURN 20000
    END

    IF EXISTS( SELECT * FROM ladderNotes WHERE notifyID = @notifyID AND noteOrder = @idx)
    BEGIN
      UPDATE ladderNotes
        SET sourceCode = @sourceCode, lastPost = @now
        WHERE notifyID = @notifyID
	AND   noteOrder = @idx
    END
    ELSE BEGIN
      INSERT ladderNotes (notifyID, noteOrder, lastPost, sourceCode)
        VALUES(@notifyID, @idx, @now, @sourceCode)
    END

    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN
      RETURN @lastError
    END
            
  COMMIT TRAN
END
GO
