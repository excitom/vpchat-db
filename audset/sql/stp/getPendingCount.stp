/*
 * Find number of pending requests to subscribe to private lists
 * owned by an account.
 *
 * A distinction may be made between a private alert list and an
 * alert list associated with a game ladder. 
 *
 * Input: Account ID
 *
 * Output: 0 = success
 *         20000 = account not found 
 */
CREATE PROC getPendingCount (
	@accountID	userIdentifier,
	@type		int = 0x00000040	-- default = private alert list
)
AS
BEGIN
  DECLARE @lastError	int

  BEGIN TRAN
    IF NOT EXISTS (SELECT * FROM vpusers..userAccounts WHERE accountID = @accountID)
    BEGIN
      ROLLBACK TRAN
      RETURN 20000
    END

    SELECT COUNT(*) AS pending
      FROM vpusers..services s, pendingListSubscrs p, notifyLists n
      WHERE accountID = @accountID
      AND (s.type = 1 OR s.type = 6)
      AND s.itemID = p.notifyID
      AND p.notifyID = n.notifyID
      AND n.type & @type = @type

    SELECT @lastError = @@error
    IF @lastError != 0
    BEGIN
      ROLLBACK TRAN
      RETURN @lastError
    END
  COMMIT TRAN
END
GO
