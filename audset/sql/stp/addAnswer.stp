/* add one guest answer to a specific interaction */
/* input:  interaction id, guest name, text
   output: Returns 20001 if original interaction's status is not ATGUEST,
                   20002 if /* sender not a predefined invitee */
*/
CREATE PROC addAnswer
(
  @interactionID	numeric(10,0),
  @senderName		VPuserID,
  @text                 varchar(255)
)
AS
BEGIN
  DECLARE @diffFromGMT int
  DECLARE @currentDate VpTime
  DECLARE @lastError int
  DECLARE @ready int
  DECLARE @atguest int
  DECLARE @interactionStatus int
  DECLARE @eventID eventIdentifier
  DECLARE @regMode int

    SELECT @ready = statusID
    FROM interactionStatus
    WHERE name = "READY"
    SELECT @lastError = @@error
    IF @lastError != 0
     RETURN @lastError
 
    SELECT @atguest = statusID
    FROM interactionStatus
    WHERE name = "ATGUEST"
    SELECT @lastError = @@error
    IF @lastError != 0
      RETURN @lastError
 
    /* Check that the original interaction exists and its status is
       "ATGUEST" */  
    SELECT @interactionStatus = status, @eventID = eventID
    FROM eventInteractions ( INDEX isPrimary )
    WHERE interactionID = @interactionID
    SELECT @lastError = @@error
    IF @lastError != 0
      RETURN @lastError
    IF @interactionStatus != @atguest
      RETURN 20001
 
    /* Check that the guest is in the list of event guests, and add "@aol"
       to his/her name if its reg mode is AOL ie. 3 */
    SELECT @regMode = regMode
      FROM eventInvitees
      WHERE inviteeName = @senderName
    SELECT @lastError = @@error
    IF @lastError != 0
      RETURN @lastError
    IF @regMode IS NULL 		/* sender not a predefined invitee */
      RETURN 20002
    ELSE IF @regMode = 3
     SELECT @senderName = @senderName + "@aol"
 
    /* Get current GMT time */
    SELECT @diffFromGMT = gmt
    FROM vpusers..getGMT
    SELECT @lastError = @@error
    IF @lastError != 0
      RETURN @lastError
    
   /* Add the Guest Answer to the database */
   INSERT INTO answers 
      ( interactionID, time, senderName, text, status)
    VALUES ( @interactionID, 
  	     dateadd( hour, (-1) * @diffFromGMT, getdate() ), 
             @senderName, @text, @ready )
    
    SELECT @lastError = @@error
    IF @lastError != 0
      RETURN @lastError
    
END
GO
